buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlin_version}"
    }
}

repositories {
    mavenCentral()
    jcenter()
}

apply plugin: 'kotlin'
apply plugin: "kotlin-kapt" // only required for generating custom actions

test {
    useJUnitPlatform()
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation platform("org.http4k:http4k-bom:$http4k_version")
    implementation platform("org.http4k:http4k-connect-bom:$http4k_connect_version")
    implementation "org.http4k:http4k-core"
    implementation "org.http4k:http4k-connect-core"
    implementation "org.http4k:http4k-format-moshi"

    implementation "org.http4k:http4k-connect-amazon-s3"
    implementation "org.http4k:http4k-connect-amazon-kms"

    kapt("org.http4k:http4k-connect-kapt-generator:2.10.0.0")

    testImplementation platform("org.junit:junit-bom:$junit_version")
    testImplementation "org.http4k:http4k-connect-core-fake"
    testImplementation "org.http4k:http4k-testing-hamkrest"
    testImplementation "org.http4k:http4k-connect-amazon-s3-fake"
    testImplementation "org.http4k:http4k-connect-amazon-kms-fake"
    testImplementation "org.junit.jupiter:junit-jupiter-api"
    testImplementation "org.junit.jupiter:junit-jupiter-engine"
    testImplementation "io.mockk:mockk:$mockk_version"
}

tasks.create("weighDependencies") {
    doLast {
        allprojects
            .forEach {
                println it.name.toUpperCase()
                it.configurations
                    .runtimeClasspath
                    .resolvedConfiguration
                    .firstLevelModuleDependencies.forEach {

                    def sorted = it.allModuleArtifacts.toSorted()
                    println "  " + it.name + " = " + sorted.sum { it.file.length() }
                    sorted.forEach {println("    " + it.file.name + " = " + it.file.length()) }
                }
            }
    }
}

compileKotlin.kotlinOptions.jvmTarget = "1.8"
compileTestKotlin.kotlinOptions.jvmTarget = "1.8"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8
